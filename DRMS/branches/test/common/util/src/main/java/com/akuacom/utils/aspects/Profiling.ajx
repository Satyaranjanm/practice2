package com.akuacom.utils.aspects;

/*
 * www.akuacom.com - Automating Demand Response
 * 
 * com.akuacom.utils.aspects.Profiling.aj - Copyright(c)1994 to 2011 by Akuacom . All rights reserved. 
 * Redistribution and use in source and binary forms, with or without modification, is prohibited.
 *
 */

import java.lang.reflect.Method;
import java.util.Arrays;
import java.util.Iterator;
import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.TimerTask;
import javax.annotation.processing.AbstractProcessor;

import org.aspectj.lang.Signature;
import org.aspectj.lang.reflect.CodeSignature;
import org.aspectj.lang.reflect.MethodSignature;

import com.akuacom.annotations.EpicMethod;
import com.akuacom.annotations.NoTrace;
import com.akuacom.annotations.DumpProfile;
import com.akuacom.utils.lang.Dbg;
import com.akuacom.utils.lang.ProfilingMaps;
import com.akuacom.utils.lang.Util;
import com.akuacom.utils.config.RuntimeSwitches;
import com.akuacom.utils.lang.StringUtil;
import com.akuacom.utils.lang.TimingUtil;

import org.junit.Test;

/**
 * This aspect tracks, with com.akuacom.utils.ProfilingMaps, the total execution
 * counts and times for every method within akuacomScope(), and outputs methods
 * blocked longer than Util.SIGMA if RuntimeSwitches.OUTPUT_PROFILING_INFO is
 * true ultra low budget profiler
 */
public aspect Profiling extends Common {
    final static int RECURSION_THRESHOLD = 5;
    final private static boolean DEBUG = false;
    final private static boolean ACTIVE = true;
    final private static boolean TRACK_JAVA_UTIL = false;
    final private static boolean TRACK_JAVA_LANG = false;
    
    pointcut scoped() :
     akuacomScope()
    && !within(com.akuacom.utils..*)
    && !within(com.akuacom.pss2.weatherInfo..*)
    && !within(com.akuacom.drms.performance..*)
    && !within(AbstractProcessor+);

    pointcut anyMethodExecution() :
      scoped()
      && !within(@NoTrace *)
      && ( execution( * *.*(..))
                || execution(new(..)))
            && !execution(public void TimerTask+.run() );

    pointcut anyJavaUtilCall() :
        call(* java.util..*(..));
    pointcut ifAnyJavaUtilCall() :
        (if(TRACK_JAVA_UTIL) && anyJavaUtilCall())
        || (if(!TRACK_JAVA_UTIL) && !anyJavaUtilCall());
        
    pointcut anyJavaLangCall() :
        call(* java.util..*(..));
    pointcut ifAnyJavaLangCall() :
        (if(TRACK_JAVA_LANG) && anyJavaLangCall())
        || (if(!TRACK_JAVA_LANG) && !anyJavaLangCall());
        
    pointcut anyMethodCall() :
    scoped()
    && call(  * *.*(..))
    && ifAnyJavaUtilCall()
    && ifAnyJavaLangCall()
    && !call(public void TimerTask+.run() )
    && !call(public static void Thread.sleep(..) )
    && !target(Exception+);

    pointcut anyHibMethodCall() :
    scoped()
    &&  call(  * org.hibernate..*(..))
    && !call(public void TimerTask+.run() )
    && !call(public static void Thread.sleep(..) )
    && !target(Exception+);

//    pointcut initializer() :
//    scoped()
//    && cflow(anyMethodCall())
//    && execution( *.new(..));

    private static String typeString(Class<?>[] types) {
        StringBuffer sb = new StringBuffer("(");
        for (Iterator<Class<?>> i = Arrays.asList(types).iterator(); i
                .hasNext();) {
            sb.append(StringUtil.dePackage(i.next().getName()));
            if (i.hasNext()) {
                sb.append(", ");
            }
        }
        sb.append(")");
        return sb.toString();
    }

    private static String methodKey(Signature sig) {
        try {
            CodeSignature cs = (CodeSignature) sig;
            String typeString = typeString(cs.getParameterTypes());
            boolean epicMethod = false;
            if (MethodSignature.class.isAssignableFrom(sig.getClass())
                    && ((MethodSignature) sig).getMethod() != null) {
                epicMethod = ((MethodSignature) sig).getMethod().getAnnotation(
                        EpicMethod.class) != null;
            }
            String methodKey;
            if (!"()".equals(typeString)) {
                methodKey = cs.toShortString().replace("(..)",
                        typeString(cs.getParameterTypes()));
            } else {
                methodKey = cs.toShortString();
            }
            if (epicMethod) {
                return EpicMethod.class.getName() + " " + methodKey;
            }
            return methodKey;
        }catch(NullPointerException npe) {
            System.out.println("NPE in Profiling.methodKey(..), ignore if this is happening during a re-deployment");
            throw npe;
        }
    }

    private static String threadNamedMethodKey(Signature sig) {
        return methodKey(sig) + Thread.currentThread();
    }

//    before() : if(ACTIVE) && anyMethodCall() && if(RuntimeSwitches.OUTPUT_PROFILING_INFO)  {
//        Signature sig = thisJoinPointStaticPart.getSignature();
//        boolean epicMethod = false;
//        if (MethodSignature.class.isAssignableFrom(sig.getClass())
//                && ((MethodSignature) sig).getMethod() != null) {
//            epicMethod = ((MethodSignature) sig).getMethod().getAnnotation(
//                    EpicMethod.class) != null;
//        }
//        String methodKey = threadNamedMethodKey(sig);
//        if(RuntimeSwitches.TRACE_EVERYTHING && !epicMethod) {
//            System.out.println("pr-calling " + methodKey);
//        }
//        
//    }
    
    before() : if(ACTIVE) && ( anyMethodExecution() || anyMethodCall() ||  anyHibMethodCall() ) && if(RuntimeSwitches.OUTPUT_PROFILING_INFO)  {
        Signature sig = thisJoinPointStaticPart.getSignature();
        boolean epicMethod = false;
        if (MethodSignature.class.isAssignableFrom(sig.getClass())
                && ((MethodSignature) sig).getMethod() != null) {
            epicMethod = ((MethodSignature) sig).getMethod().getAnnotation(
                    EpicMethod.class) != null;
        }
        String methodKey = threadNamedMethodKey(sig);
        if(RuntimeSwitches.TRACE_EVERYTHING && !epicMethod) {
            System.out.println("pr-enter " + methodKey);
        }
        try {
            Map<String, List<Long>> map = ProfilingMaps.getDeltaMap();
            List<Long> times = map.get(methodKey);
            if (times == null) {
                times = new ArrayList<Long>();
                times.add(System.currentTimeMillis());
                try {
                    map.put(methodKey, times);
                } catch (NullPointerException e) {
                    if (methodKey == null || map == null || times == null) {
                        System.out.println("methodKey " + (methodKey == null) + ", map "
                                + (map == null) + ", times " + (times == null));
                        e.printStackTrace();
                    }
                }
            } else {
                if (times.size() > RECURSION_THRESHOLD) {
                    System.out.println(methodKey + " recursed " + times.size());
                }
                times.add(System.currentTimeMillis());
            }
        } catch (NullPointerException n) {
            System.out.println("Profiling.aj got NPE");
        }
        System.out.flush();
    }

    after() : if(ACTIVE) && ( anyMethodExecution() || anyMethodCall() || anyHibMethodCall() ) && if(RuntimeSwitches.OUTPUT_PROFILING_INFO) {
        Signature sig = thisJoinPointStaticPart.getSignature();
        boolean epicMethod = false;
        if (MethodSignature.class.isAssignableFrom(sig.getClass())
                && ((MethodSignature) sig).getMethod() != null) {
            epicMethod = ((MethodSignature) sig).getMethod().getAnnotation(
                    EpicMethod.class) != null;
        }
        String methodKey = threadNamedMethodKey(sig);
        if(RuntimeSwitches.TRACE_EVERYTHING && !epicMethod) {
            System.out.println("pr-exit " + methodKey);
        }

        String methodKeyForAggregateTimes = methodKey(sig);
        try {
            Map<String, List<Long>> map = ProfilingMaps.getDeltaMap();
            List<Long> times = map.get(methodKey);
            long ltime = 0;
            double time = 0;
            try {
                ltime = times.remove(times.size() - 1);
                time = (System.currentTimeMillis() - ltime) / 1000.;
            } catch(ArrayIndexOutOfBoundsException ex) {
                System.out.println("bad idx " + (times.size() - 1));
                return;
            }
            if (ProfilingMaps.AGGREGATE_METHOD_TIMES) {
                Double dTotalTime = ProfilingMaps.getAggregateMap().get(
                        methodKeyForAggregateTimes);
                if (dTotalTime == null) {
                    dTotalTime = time;
                } else {
                    dTotalTime += time;
                }
                Long count = ProfilingMaps.getCounterMap().get(
                        methodKeyForAggregateTimes);
                if (count == null) {
                    count = 1l;
                } else {
                    count++;
                }
                ProfilingMaps.getAggregateMap().put(methodKeyForAggregateTimes,
                        dTotalTime);
                ProfilingMaps.getCounterMap().put(methodKeyForAggregateTimes,
                        count);
            }
            if (time > Util.SIGMA
                    && methodKey.indexOf(EpicMethod.class.getName()) == -1) {
                System.out.println(methodKey + " took "
                        + TimingUtil.fromSeconds(time));
            }
        } catch (NullPointerException e) {
            if (DEBUG)
                System.out.println("mismatched before / after for " + methodKey);
        } 
        System.out.flush();
    }

//    after() : if(ACTIVE) &&  initializer() && if(RuntimeSwitches.OUTPUT_PROFILING_INFO) {
//
//        String methodKeyForAggregateTimes = methodKey(thisEnclosingJoinPointStaticPart
//                .getSignature())
//                + ": "
//                + methodKey(thisJoinPointStaticPart.getSignature());
//        Long count = ProfilingMaps.getInitializerMap().get(
//                methodKeyForAggregateTimes);
//        if (count == null) {
//            count = 1l;
//        } else {
//            count++;
//        }
//        ProfilingMaps.getInitializerMap()
//                .put(methodKeyForAggregateTimes, count);
//    }

    after() : if(ACTIVE) && execution(@Test * *.*(..)) {
        Signature sig = thisJoinPointStaticPart.getSignature();
        if (sig instanceof MethodSignature) {
            Method method = ((MethodSignature) sig).getMethod();
            DumpProfile dump = method.getAnnotation(DumpProfile.class);
            if ((RuntimeSwitches.OUTPUT_PROFILE_AFTER_TEST || dump != null) && RuntimeSwitches.OUTPUT_PROFILING_INFO) {
                System.out.println("Profiling.aj @Test" + (dump != null ? " @DumpProfile" : ""));
                ProfilingMaps.printTopX(200);
            } 
        }
    }
}